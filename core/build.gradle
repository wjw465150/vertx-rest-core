//打包: 进入此模块目录执行`..\gradlew -x test clean build` 成功后发布:`..\gradlew -x test publish -Dorg.gradle.internal.http.socketTimeout=200000 -Dorg.gradle.internal.http.connectionTimeout=200000`
//然后登录https://oss.sonatype.org/#stagingRepositories来查看,你的提交在未处理前，是`open`状态，然后点击`Close`按钮;然后等一会点击`Release`来发布
plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing' //使用signing plugin做数字签名
}

apply from: rootDir.canonicalPath + '/.gradle/publish.gradle'

// jar包的名字
archivesBaseName = 'vertx-rest-core'

group = 'com.github.wjw465150'  //Sonatype上的Issue里填写的`Group Id`,可以跟package路径完全不一样
version = '1.2.0'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

description = """WJW的一个Vert.x 4 的 Rest Core 扩展库"""

compileJava {
  options.encoding = 'UTF-8'
  options.deprecation = true
  options.compilerArgs += ["-parameters","-Xlint:deprecation"]
  
  doFirst {
    println "options:"+options
    println "options.compilerArgs:"+options.compilerArgs
  }
  
}

compileTestJava {
  options.encoding = 'UTF-8'
  options.deprecation = true
  options.compilerArgs += ["-parameters","-Xlint:deprecation"]
          
  doFirst {
    println "options:"+options
    println "options.compilerArgs:"+options.compilerArgs
  }
  
}

jar {
  manifest {
    attributes 'Built-By': 'wjw465150@gmail.com',
    'Build-Name': "${project.archivesBaseName}",
    'Build-Version': "${project.version}",
    'Build-URL': 'https://github.com/wjw465150/vertx-rest-core'
  }
  excludes = ['**/test/**']
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  // 定义发布什么
  publications {
    mavenJava(MavenPublication) {
      // groupId,artifactId,version，如果不定义，则会按照默认值执行
      groupId = project.group  //Sonatype上的Issue里填写的`Group Id`,可以跟package路径完全不一样
      artifactId = project.archivesBaseName
      version = project.version
 
      from components.java
      versionMapping {  //为了解决: Gradle, SpringBoot, MavenPublish - Publication only contains dependencies and/or constraints without a version
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      
      pom {
        // 构件名称
        // 区别于artifactId，可以理解为artifactName
        name = project.archivesBaseName
        // 构件描述
        description = 'vertx-rest-core是基于Vertx的实现了REST的一个核心库'
        // 构件主页
        url = 'https://github.com/wjw465150/vertx-rest-core'
        // 许可证名称和地址
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'vertx-rest-core是基于Vertx的实现了REST的一个核心库'
          }
        }
        // 开发者信息
        developers {
          developer {
            id = 'wjw465150'
            name = 'wjw465150'
            email = 'wjw465150@gmail.com'
          }
        }
        // 版本控制仓库地址
        scm {
          url = "https://github.com/wjw465150/vertx-rest-core"
          connection = "https://github.com/wjw465150/vertx-rest-core.git"
          developerConnection = "https://github.com/wjw465150/vertx-rest-core.git"
        }
      }
    }
  }
 
  // 定义发布到哪里
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        // 这里就是之前在issues.sonatype.org注册的账号,这些敏感信息为了防止泄露,我放到了`.gradle/publish.gradle`目录下
        username sonatypeUsername
        password sonatypePassword
      }
    }
  }
}
 
//为所有的jar包做数字签名
signing {
  sign publishing.publications.mavenJava
}
 
repositories {
  mavenLocal()
  maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }  //优先使用阿里的镜像
  mavenCentral()
}

def vertxVersion = "4.2.7"

dependencies {
    implementation platform("io.vertx:vertx-stack-depchain:$vertxVersion")  //Vert.x Stack Depchain,集中了Vert.x的依赖版本管理,这样后面在导入Vert.x模块时就不必再填写版本号了!
    
    //如果项目的Jar包要对外暴露，就用api
    api "io.vertx:vertx-core"
    api "io.vertx:vertx-web"
    api "io.vertx:vertx-codegen"
    api "io.vertx:vertx-service-proxy"
    
    api "com.fasterxml.jackson.core:jackson-databind"

    api "org.slf4j:slf4j-api:1.7.36"
    api "org.reflections:reflections:0.10.2"
}

compileJava.dependsOn(processResources)
